apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretProperties = new Properties()

if (secretsPropertiesFile.exists())
    secretProperties.load(new FileInputStream(secretsPropertiesFile))
else {
    secretProperties.setProperty("google_project_id", "\"${System.getenv('google_project_id') ?: "null"}\"")
    secretProperties.setProperty("signing_keystore_password", "${System.getenv('signing_keystore_password')}")
    secretProperties.setProperty("signing_key_password", "${System.getenv('signing_key_password')}")
    secretProperties.setProperty("signing_key_alias", "${System.getenv('signing_key_alias')}")
}


android {
    buildFeatures.viewBinding = true
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.javinator9889.calculator"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 16
        versionName "1.0.6"
        resConfigs 'en', 'es'
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        /*
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }*/
    }
    signingConfigs {
        release {
            storeFile file("../android-signing-keystore.jks")
            storePassword "${secretProperties['signing_keystore_password']}"
            keyAlias "${secretProperties['signing_key_alias']}"
            keyPassword "${secretProperties['signing_key_password']}"
        }
    }

    buildTypes {
        debug {
            ext.enableCrashliytics = false
            versionNameSuffix "-debug"
            debuggable true
            multiDexEnabled false
        }
        beta {
            initWith(debug)
            versionNameSuffix "-beta"
            minifyEnabled true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            multiDexEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexEnabled false
            shrinkResources true
            signingConfig signingConfigs.release
            versionNameSuffix "-stable"

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            zipAlignEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    /*externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }*/
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // https://developer.android.com/kotlin/ktx#lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    // https://developer.android.com/kotlin/ktx#viewmodel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    // https://developer.android.com/kotlin/ktx#livedata
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    // https://github.com/JakeWharton/timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    // https://developer.android.com/jetpack/androidx/releases/lifecycle#declaring_dependencies
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.2.0'
    implementation files("libs/arity-2.1.7.jar")
    // https://firebase.google.com/docs/android/setup#add-sdks
    implementation 'com.google.firebase:firebase-crashlytics:17.1.1'
}
